
SET(ADM_LIB ADM_render6)
ADD_LIBRARY(${ADM_LIB} SHARED )

IF(QT_LIBRARY_EXTENSION)
  SET_TARGET_PROPERTIES(${ADM_LIB} PROPERTIES OUTPUT_NAME ADM_render6_${QT_LIBRARY_EXTENSION})
  TARGET_SOURCES(${ADM_LIB} PRIVATE ${CMAKE_CURRENT_SOURCE_DIR}/GUI_render.cpp ${CMAKE_CURRENT_SOURCE_DIR}/GUI_simpleRender.cpp)
  TARGET_LINK_LIBRARIES(${ADM_LIB} PUBLIC ${QT_QTGUI_LIBRARY})
ELSE()
  SET_TARGET_PROPERTIES(${ADM_LIB} PROPERTIES OUTPUT_NAME ADM_render6_${UI_SUFFIX})
  TARGET_SOURCES(${ADM_LIB} PRIVATE ${CMAKE_CURRENT_SOURCE_DIR}/GUI_render.cpp ${CMAKE_CURRENT_SOURCE_DIR}/GUI_simpleRenderStub.cpp)
ENDIF()

IF(NOT ${UI_SUFFIX} STREQUAL "cli")
  IF(USE_OPENGL)
    include(admOpenGl)
    INCLUDE_DIRECTORIES(${CMAKE_SOURCE_DIR}/ADM_openGL/include/)
    ADM_OPEN_GL(${ADM_LIB})
    TARGET_SOURCES(${ADM_LIB} PRIVATE ${CMAKE_CURRENT_SOURCE_DIR}/GUI_qtGlRender.cpp)
  ENDIF()

  IF(APPLE)
    IF(USE_SDL)
      TARGET_SOURCES(${ADM_LIB} PRIVATE GUI_sdlRenderHelper.m)
    ENDIF()
  ENDIF()
  IF(USE_XV)
    TARGET_SOURCES(${ADM_LIB} PRIVATE GUI_xvRender.cpp)
    TARGET_LINK_LIBRARIES(${ADM_LIB} PUBLIC ${XVIDEO_LIBRARY_DIR} ${XEXT_LIBRARY_DIR})
  ENDIF()
  IF(USE_SDL)
    TARGET_SOURCES(${ADM_LIB} PRIVATE GUI_sdlRender.cpp)
    SDLify(GUI_sdlRender.cpp)
    TARGET_LINK_LIBRARIES(${ADM_LIB} PUBLIC ${SDL2_LIBRARY})
  ENDIF()
  IF(USE_VDPAU)
    ADD_CORE_INCLUDE(ADM_coreVdpau)
    TARGET_SOURCES(${ADM_LIB} PRIVATE GUI_vdpauRender.cpp)
    TARGET_LINK_LIBRARIES(${ADM_LIB} PUBLIC ADM_coreVDPAU6)
  ENDIF()
  IF(USE_DXVA2)
    ADD_SUBDIRECTORY(ADM_coreDxva2)
    INCLUDE_DIRECTORIES(ADM_coreDxva2/include)
    TARGET_LINK_LIBRARIES(${ADM_LIB} PUBLIC ADM_coreDxva26)
    ADD_CORE_INCLUDE(ADM_coreDxva2)
    TARGET_SOURCES(${ADM_LIB} PRIVATE GUI_dxva2Render.cpp)
  ENDIF()
  IF(USE_LIBVA)
    ADD_CORE_INCLUDE(ADM_coreLibVA)
    TARGET_SOURCES(${ADM_LIB} PRIVATE GUI_libvaRender.cpp)
  ENDIF()
ENDIF() # Not cli

TARGET_LINK_LIBRARIES(${ADM_LIB} PUBLIC ADM_core6 ADM_coreUI6 ADM_coreImage6 ADM_coreUtils6)
TARGET_INCLUDE_DIRECTORIES(${ADM_LIB} PUBLIC ${CMAKE_CURRENT_SOURCE_DIR})

ADD_LIBRARY(ADM_render6_interface INTERFACE)
TARGET_INCLUDE_DIRECTORIES(ADM_render6_interface INTERFACE ${CMAKE_CURRENT_SOURCE_DIR})


# ?? ADM_COND_LINK(GETTEXT_FOUND ${GETTEXT_LIBRARY_DIR})
ADM_INSTALL_LIB(${ADM_LIB})
# EOF
