INCLUDE(admCheckFreeType)
INCLUDE(admCheckFribidi)
INCLUDE(admCheckFontConfig)
ADD_CORE_INCLUDE(ADM_coreSubtitles)
checkFreeType()
checkFribidi("0.19")


IF((USE_FREETYPE AND USE_FRIBIDI) OR MSVC)
  INCLUDE(vf_plugin)
  ADD_VIDEO_FILTER(ADM_vf_ssa ADM_vidASS.cpp)

  IF(DO_COMMON)
    IF(USE_EXTERNAL_LIBASS OR MSVC)
      MESSAGE(STATUS "Checking libass external lib ...")
      PKG_CHECK_MODULES(LIBASS libass)
      IF(LIBASS_FOUND)
        TARGET_COMPILE_DEFINITIONS(ADM_vf_ssa PRIVATE "USE_EXTERNAL_LIBASS")
        TARGET_COMPILE_OPTIONS(ADM_vf_ssa PRIVATE ${LIBASS_CFLAGS})
        SET(LIBASS_LIBRARIES ${LIBASS_LDFLAGS})
        MESSAGE(STATUS "   found, lib is ${LIBASS_LIBRARIES}")
      ELSE()
        MESSAGE(FATAL_ERROR "Cant locate libass ! cancelling")
      ENDIF()
      TARGET_LINK_LIBRARIES(ADM_vf_ssa PRIVATE ${LIBASS_LIBRARIES} adm_freetype adm_fribidi  ADM_coreSubtitles6)
    ELSE()
      ADD_SUBDIRECTORY(ADM_libass)
      TARGET_LINK_LIBRARIES(ADM_vf_ssa PRIVATE ADM_libass adm_freetype  adm_fribidi  ADM_coreSubtitles6)
    ENDIF()
    # We force the use of fontconfig
    TARGET_COMPILE_DEFINITIONS(ADM_vf_ssa PRIVATE "USE_FONTCONFIG")
    TARGET_LINK_LIBRARIES(ADM_vf_ssa PRIVATE adm_fontconfig)
    # Add a simple probe program, only on linux/bsd/...
    IF(WIN32 OR APPLE)
    ELSE()
      ADD_EXECUTABLE(freetype_probe probe.cpp)
      TARGET_LINK_LIBRARIES(freetype_probe PRIVATE adm_freetype  adm_fribidi  adm_fontconfig )
    ENDIF()
  ENDIF()

  INIT_VIDEO_FILTER(ADM_vf_ssa)
  INSTALL_VIDEO_FILTER(ADM_vf_ssa)
ELSE()
  MESSAGE(STATUS "Fribidi or feetype missing, skipping filter")
ENDIF()
